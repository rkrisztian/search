plugins {
	id 'groovy'
	id 'application'
	id 'codenarc'
	id 'jacoco'
	id 'jacoco-report-aggregation'
}

wrapper {
	gradleVersion = '8.14.4'
	distributionType = Wrapper.DistributionType.ALL
}

version = '3.0.0'
group = 'rkrisztian.search'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation libs.groovy.all
	codenarc libs.codenarc
	codenarc libs.groovy.all
}

testing {
	suites {
		configureEach {
			useJUnitJupiter()

			dependencies {
				implementation libs.spock.core
			}

			targets.all {
				testTask.configure() {
					testLogging { events 'skipped', 'failed' }
				}
			}
		}

		integrationTest(JvmTestSuite) {
			dependencies {
				implementation project()
				implementation configurations.testCompileClasspath
				implementation sourceSets.test.output
			}

			targets.all {
				testTask.configure {
					systemProperty 'java.io.tmpdir', "${-> layout.buildDirectory.dir('tmp').get()}"
				}
			}
		}
	}
}

application {
	applicationName = 'search'
	mainClass = 'search.Search'
}

distributions {
	main {
		contents {
			from('README.md') {
				into 'docs'
			}
		}
	}
}

tasks.named('jar') {
	manifest {
		attributes 'Implementation-Title': 'Search App',
				'Implementation-Version': archiveVersion
	}
}

codenarc {
	configFile = file 'config/codenarc/rules.groovy'
	reportFormat = 'html'
}

jacoco {
	toolVersion = '0.8.14'
}

tasks.named('jacocoTestReport') {
	enabled = false
}

tasks.register('codeCoverageReport', JacocoReport) {
	sourceSets sourceSets.main

	tasks.matching({ t -> t.extensions.findByType(JacocoTaskExtension) }).forEach { testTask ->
		executionData testTask
		dependsOn testTask
	}

	reports {
		xml.required = true
		html.required = true
	}
}

tasks.named('check') {
	dependsOn testing.suites.integrationTest
	dependsOn 'codeCoverageReport'
}
