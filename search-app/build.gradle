apply plugin: 'groovy'

dependencies {
	implementation 'org.codehaus.groovy:groovy-all:2.4.15'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

task fatJar(type: Jar) {
	dependsOn jar
	baseName = "${project.name}-standalone"

	manifest {
		attributes 'Implementation-Title': 'Search App (Standalone)',
				'Implementation-Version': version,
				'Main-Class': 'search.Search'
	}

	from {
		configurations.compileClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	with jar
}

task generateSearchScript(type: Copy) {
	from 'etc/search_tmpl'
	into "${buildDir}/${distsDirName}"
	rename { file -> 'search' }
	expand System: System, fatJar: fatJar
}

task setSearchScriptPerms(type: Exec) {
	commandLine 'chmod', '+x', file("${buildDir}/${distsDirName}/search")
}

generateSearchScript.finalizedBy setSearchScriptPerms
assemble.dependsOn fatJar, generateSearchScript

test {
	useJUnitPlatform()
	testLogging {
		events 'skipped', 'failed'
	}
}
