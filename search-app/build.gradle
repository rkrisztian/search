plugins {
	id 'groovy'
	id 'org.unbroken-dome.test-sets' version '2.1.1'
}

dependencies {
	implementation 'org.codehaus.groovy:groovy-all:2.5.7'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

task fatJar(type: Jar) {
	dependsOn jar
	baseName = "${project.name}-standalone"

	manifest {
		attributes 'Implementation-Title': 'Search App (Standalone)',
				'Implementation-Version': version,
				'Main-Class': 'search.Search'
	}

	from {
		configurations.compileClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	with jar
}

task generateSearchScript(type: Copy) {
	finalizedBy 'setSearchScriptPerms'
	from 'etc/search_tmpl'
	into "${buildDir}/${distsDirName}"
	rename { file -> 'search' }
	expand System: System, fatJar: fatJar
}

task setSearchScriptPerms(type: Exec) {
	commandLine 'chmod', '+x', file("${buildDir}/${distsDirName}/search")
}

assemble.dependsOn fatJar, generateSearchScript

testSets {
	integrationTest
}

check.dependsOn integrationTest

[test, integrationTest].each { task ->
	task.with {
		useJUnitPlatform()
		testLogging {
			events 'skipped', 'failed'
		}
	}
}
