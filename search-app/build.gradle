plugins {
	id 'groovy'
	id 'application'
	id 'org.unbroken-dome.test-sets' version '3.0.1'
	id 'codenarc'
	id 'jacoco'
}

testSets {
	integrationTest
}

dependencies {
	def groovyVersion = '3.0.8'
	def junitVersion = '5.7.2'

	implementation platform("org.codehaus.groovy:groovy-bom:$groovyVersion")
	implementation "org.codehaus.groovy:groovy-all:$groovyVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	codenarc 'org.codenarc:CodeNarc:2.1.0'
	codenarc "org.codehaus.groovy:groovy-all:$groovyVersion"
	integrationTestImplementation sourceSets.test.output
}

application {
	applicationName = 'search'
	mainClass = 'search.Search'
}

distributions {
	main {
		contents {
			from("$rootDir/README.md") {
				into 'docs'
			}
		}
	}
}

tasks.named('jar').configure {
	manifest {
		attributes 'Implementation-Title': 'Search App',
				'Implementation-Version': archiveVersion
	}
}

tasks.named('check').configure {
	dependsOn 'integrationTest'
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
	testLogging { events 'skipped', 'failed' }
	finalizedBy "jacoco${it.name.capitalize()}Report"
}

tasks.withType(JacocoReport).configureEach {
	reports {
		xml.enabled true
	}
}

codenarc {
	configFile = file 'config/codenarc/rules.groovy'
	reportFormat = 'html'
}

jacoco {
	toolVersion = '0.8.7'
}
