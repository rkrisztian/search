plugins {
	id 'groovy'
	id 'org.unbroken-dome.test-sets' version '3.0.1'
	id 'codenarc'
}

dependencies {
	implementation 'org.codehaus.groovy:groovy-all:2.5.7'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

tasks.register('fatJar', Jar) {
	dependsOn 'jar'
	archiveBaseName = "${project.name}-standalone"

	manifest {
		attributes 'Implementation-Title': 'Search App (Standalone)',
				'Implementation-Version': archiveVersion,
				'Main-Class': 'search.Search'
	}

	from {
		configurations.compileClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}

	with jar
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('generateSearchScript', Copy) {
	from 'etc/search_tmpl'
	into "${buildDir}/${distsDirName}"
	rename { file -> 'search' }
	expand System: System, fatJar: fatJar
	fileMode = 0755
}

tasks.named('assemble').configure {
	dependsOn 'fatJar', 'generateSearchScript'
}

testSets {
	integrationTest
}

tasks.named('check').configure {
	dependsOn 'integrationTest'
}

['test', 'integrationTest'].each { taskName ->
	tasks.named(taskName) {
		useJUnitPlatform()
		testLogging {
			events 'skipped', 'failed'
		}
	}
}

codenarc {
	configFile = file 'config/codenarc/rules.groovy'
	reportFormat = 'html'
}
